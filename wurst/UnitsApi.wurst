package UnitsApi

import Abilities
import HashList
import Icons
import Units
import UnitObjEditing

class UnitProps
    int id
    string model
    string name
    real scale_size
    ArmorType armor_type

    function setup(int id, string model, string name, ArmorType armor_type, real scale_size)
        this.id = id
        this.model = model
        this.name = name
        this.scale_size = scale_size
        this.armor_type = armor_type

    construct(int id, string model, ArmorType armor_type, string name)
        setup(id, model, name, armor_type, 0.5)

    construct(int id, string model, ArmorType armor_type, string name, real scale_size)
        setup(id, model, name, armor_type, scale_size)


public constant SHIP_PROPS = new HashList<UnitProps>()..add(
    new UnitProps('wv00', Units.orcishTransportShip, ArmorType.Large, "Dinghy", .4),
    new UnitProps('wv01', Units.humanTransportShip1, ArmorType.Medium, "Sailor"),
    new UnitProps('wv02', Units.humanDestroyerShip1, ArmorType.Small, "Interceptor"),
    new UnitProps('wv03', Units.nightElfTransportShip1, ArmorType.Unarmored, "Crusader"),
    new UnitProps('wv04', Units.orcishDestroyerShip1, ArmorType.Fortified, "Sea Punisher"),
    new UnitProps('wv05', Units.undeadDestroyerShip1, ArmorType.Medium, "Destroyer"),
    new UnitProps('wv06', Units.nightElfBattleship, ArmorType.Fortified, "Dominator"),
    new UnitProps('wv07', Units.orcJuggernaught, ArmorType.Large, "Juggernaught"),
    new UnitProps('wv08', Units.undeadShipBattleShip, ArmorType.Medium, "Overlord"),
    new UnitProps('wv09', Units.undeadTransportShip1, ArmorType.Small, "Ghost ship"),
    new UnitProps('wv10', Units.nightElfFishingBoat, ArmorType.Unarmored, "Fishing boat"),
    new UnitProps('wv11', Units.nightElfDestroyerShip1, ArmorType.Large, "Battleship"),
    new UnitProps('wv12', Units.humanBattleship, ArmorType.Medium, "Dreadnought"),
    new UnitProps('wv13', Units.warWagon, ArmorType.Fortified, "Tank"),
    new UnitProps('wv14', Units.bloodElfWagon, ArmorType.Unarmored, "Elven treasure wagon"),
    new UnitProps('wv15', Units.obsidianStatue1, ArmorType.Small, "Floating artifact"),
    new UnitProps('wv16', Units.undeadAirBarge1, ArmorType.Medium, "Yamato"),
    new UnitProps('wv17', Units.wisp1, ArmorType.Hero, "Spirit", 1.),
    new UnitProps('wv18', Units.goblinZeppelin, ArmorType.Small, "Goblin warship", .75),
    new UnitProps('wv19', Units.orcishTransportShip, ArmorType.Large, "Agamemnon", .75)

    // The game must be more interesting before adding more waves...
)

public constant ID_DEFENDER_0 = 'df00'
public constant ID_DEFENDER_1 = 'df01'
public constant ID_DEFENDER_2 = 'df02'
public constant ID_DEFENDER_3 = 'df03'
public constant ID_MELEE_DEF_0 = 'dfm0'
public constant ID_MELEE_DEF_1 = 'dfm1'
public constant ID_MELEE_DEF_2 = 'dfm2'
public constant ID_MELEE_DEF_3 = 'dfm3'
public constant ID_DONOTHING_DEF = 'dfnt'

public constant ID_BARRACKS = 'raxx'

class BaseShip extends UnitDefinition
    construct(int id, real scale, int ss, int hp, int bounty, string name, string model, ArmorType armor_type, int armor_amt)
        super(id, 'hbot')
        setScalingValue(scale)
        setSpeedBase(ss)
        setArmorType(armor_type)
        setModelFile(model)
        setHitPointsMaximumBase(hp)
        setName(name)
        setGoldBountyAwardedBase(bounty - 1)
        setGoldBountyAwardedNumberofDice(1)
        setGoldBountyAwardedSidesperDie(1)
        setCollisionSize(15.)  // 7 too small.
        setAttacksEnabled(1)
        setAttack1AttackType(AttackType.Normal)
        setAttack1Range(250)
        setAttack1TargetsAllowed("ground,structure,debris,air,item,ward")
        setAttack1ProjectileArt(Abilities.cannonTowerMissile)
        setAttack1ProjectileSpeed(800)
        setAttack1CooldownTime(1.)
        setAttack1DamageBase(499)
        setAttack1DamageNumberofDice(1)
        setAttack1DamageSidesperDie(1)
        setAttack1MaximumNumberofTargets(1)
        setAttack1WeaponType(WeaponType.Missile)
        setSelectionScale(2.)
        setNormalAbilities("")
        setDefenseBase(armor_amt)
        setGoldCost(0)

public function wave_count() returns int
    return SHIP_PROPS.size()

@compiletime function createShips()
    for i = 0 to (wave_count() - 1)
        let props = SHIP_PROPS.get(i)
        new BaseShip(
            props.id,
            props.scale_size,
            140 + 5 * i,
            2 * (85. * 1.15.pow(i.toReal())).toInt(),  // 90 seemed too high.
            8 + i,  // 7 seemed too low.
            props.name,
            props.model,
            props.armor_type,
            1 + (i / 5.).toInt()
        )


class BaseDefender extends UnitDefinition
    function setup(int baseDamage, int range, int cost, string name, string sound_set, WeaponType wt, string projectileArt, string model, string icon)
        setButtonPositionX(0)
        setButtonPositionY(0)
        setAttack1DamageBase(baseDamage)
        setAttack1Range(range)
        setModelFile(model)
        setGoldCost(cost)
        setStockMaximum(1)
        setStockReplenishInterval(0)
        setStockStartDelay(0)
        setBuildTime(1)
        setAcquisitionRange(range.toReal())
        setLumberCost(0)
        setFoodCost(1)
        setName(name)
        setTooltipBasic("Train " + name)
        setTooltipExtended(
            "A ranged, mobile defender which can hold position and form a blockade.|n |n" +
            "|cffffcc00Nominal damage:|r " + (baseDamage + 1).toString() + "|n" +
            "|cffffcc00Attack type:|r Piercing"
        )
        setIconGameInterface(icon)
        setCollisionSize(32.) // 31 too small.
        setAttack1CooldownTime(1.25)
        setSelectionScale(1.5)
        setAttack1WeaponType(wt)
        setUnitSoundSet(sound_set)
        setAttack1ProjectileArt(projectileArt)

    construct(int id, int baseDamage, int range, int cost, string name, string sound_set, WeaponType wt, string projectileArt, string model, string icon)
        super(id, 'nhea')
        setup(baseDamage, range, cost, name, sound_set, wt, projectileArt, model, icon)

    construct(int id, int baseDamage, int range, int cost, string name, string sound_set, string model, string icon)
        super(id, 'nhea')
        setup(baseDamage, range, cost, name, sound_set, WeaponType.Missile, Abilities.arrowMissile1, model, icon)

class BaseMeleeDefender extends UnitDefinition
    construct(int id, int baseDamage, int cost, string name, string sound_set, string model, string icon)
        super(id, 'hfoo')
        setButtonPositionX(0)
        setButtonPositionY(1)
        setAttack1DamageBase(baseDamage)
        setNormalAbilities("")
        setAttack1Range(100)
        setModelFile(model)
        setGoldCost(cost)
        setStockMaximum(1)
        setStockReplenishInterval(0)
        setStockStartDelay(0)
        setBuildTime(1)
        setUnitSoundSet(sound_set)
        setAcquisitionRange(100)
        setLumberCost(0)
        setFoodCost(1)
        setName(name)
        setTooltipBasic("Train " + name)
        setTooltipExtended(
            "A melee, mobile defender which can hold position and form a blockade.|n |n" +
            "|cffffcc00Nominal damage:|r " + (baseDamage + 1).toString() + "|n" +
            "|cffffcc00Attack type:|r Normal"
        )
        setIconGameInterface(icon)
        setCollisionSize(32.) // 31 too small.
        setAttack1CooldownTime(1.25)
        setSelectionScale(1.5)

class DoNothingDefender extends BaseMeleeDefender
    construct(int id, int cost, string name, string sound_set, string model, string icon)
        super(id, 0, cost, name, sound_set, model, icon)
        setAttacksEnabled(0)
        setTooltipExtended(
            "A mobile defender which can hold position and form a blockade.|n |n" +
            "|cffcc0000Does not attack.|r "
        )

@compiletime function createDefenders()
    new BaseDefender(ID_DEFENDER_0,  10, 400,  50, "Elven Archer", "Archer", Units.highElfArcher1, Icons.bTNHighElvenArcher)
    new BaseDefender(ID_DEFENDER_1,  25, 410, 100, "Elven Ranger", "Archer", Units.archer, Icons.bTNArcher)
    new BaseDefender(ID_DEFENDER_2,  55, 420, 200, "Dwarven Rifleman", "Rifleman", WeaponType.Instant, Abilities.rifleImpact, Units.rifleman1, Icons.bTNRifleman)
    new BaseDefender(ID_DEFENDER_3, 115, 430, 400, "Space Marine", "TerranMarine", WeaponType.Instant, Abilities.rifleImpact, Units.marine, Icons.bTNMarine)

    new BaseMeleeDefender(ID_MELEE_DEF_0,  15,  50, "Human Footman", "Footman", Units.footman1, Icons.bTNFootman)
    new BaseMeleeDefender(ID_MELEE_DEF_1,  35, 100, "Elven Swordsman", "HighElfSwordsman", Units.theCaptain1, Icons.bTNTheCaptain)
    new BaseMeleeDefender(ID_MELEE_DEF_2,  75, 200, "Elven Warden", "Assassin", Units.assassin1, Icons.bTNAssassin)
    new BaseMeleeDefender(ID_MELEE_DEF_3, 155, 400, "Human Cavalry", "Bandit", Units.banditLord1, Icons.bTNBanditLord)

    new DoNothingDefender(ID_DONOTHING_DEF, 5, "Human Villager", "VillagerWoman", Units.villagerWoman1, Icons.bTNVillagerWoman)

class Barracks extends BuildingDefinition
    construct(int id)
        super(id, 'hbar')
        setFoodProduced(40)
        setUnitsTrained("")
        setUnitsSold("df00,df01,df02,df03,dfm0,dfm1,dfm2,dfm3,dfnt")
        setResearchesAvailable("")
        setPlacementRequires("")

@compiletime function createBarracks()
    new Barracks(ID_BARRACKS)
